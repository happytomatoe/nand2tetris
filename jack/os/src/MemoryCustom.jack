class MemoryCustom {
    static Array ram;
    static Array heap;
    static int heapTail, heapBase;

    function void init(){
        let heapBase = 2048;
        let ram = 0;
        let heap = heapBase;
        let heap[0] = 0;
        let heap[1] = 14334;
        let heapTail = 0;
        return;
    }
    //returns the value of the main memory at this address.
    function int peek(int address){
        return ram[address];
    }
    //sets the contents of the main memory at this address to value.
    function void poke(int address, int value){
        let ram[address] = value;
        return;
    }
    //finds and allocates from the heap a memory block of the specified size and returns a reference to its base address.
    function Array alloc(int size){
        var boolean segmentFound;
        var int heapSize, segmentIndex, newSegmentStart;
        if (~(heap = 2048)){
            do Output.printString("Unexpected heap starts memory location. Did you forget to call init()?");
            do Sys.halt();
        }
        if (size < 1){
            do Sys.error(5);
        }
        let segmentFound = false;
        //find segment using first fit
        //check first segment
        let segmentIndex = 0;
        if (heap[segmentIndex + 1] > (size + 1)){
            let segmentFound = true;
        } else {
            let segmentIndex = heap[segmentIndex];
        }
        // check rest
        while (~(segmentIndex = 0) & ~segmentFound){
            if (heap[segmentIndex + 1] > (size + 1)){
                let segmentFound = true;
            } else {
                let segmentIndex = heap[segmentIndex];
            }
        }
        if (~segmentFound){
            do Sys.error(6);
        }
        //carve out segment
        let heap[segmentIndex + 1] = heap[segmentIndex + 1] - size - 2;
        //+2 because previous segment has overhead which consists of 2 elements
        let newSegmentStart = heap[segmentIndex + 1] + 2;
        let heap[newSegmentStart] = 0;
        let heap[newSegmentStart + 1] = size;
        return heapBase + newSegmentStart + 2;
    }
    //De-allocates the given object and frees its memory space.
    function void deAlloc(Array o){
        let heap[heapTail] = o - 2;
        let heapTail = o - heapBase - 2;
        return;
    }
}
