// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zero the x input?
    Not16(in= x, out=notX );
    And16(a= x, b=notX, out=zero);
    Mux16(a= x, b=zero, sel=zx, out=zeroedX);
    // negate the x input?
    Not16(in=zeroedX, out=notZeroedX);
    Mux16(a=zeroedX, b=notZeroedX, sel=nx, out=finalX);
    
    // zero the y input?
    Mux16(a= y, b=zero, sel=zy, out=zeroedY);
    // negate the y input?
    Not16(in=zeroedY, out=notZeroedY);
    Mux16(a=zeroedY, b=notZeroedY, sel=ny, out=finalY);

    
    // compute (out = x + y) or (out = x & y)?
    Add16(a=finalX, b=finalY, out=xPlusY);
    And16(a=finalX, b=finalY, out=xAndY);
    Mux16(a=xAndY, b=xPlusY, sel=f, out=fOut );
    // negate the out output?
    Not16(in=fOut, out=notFOut);
    Mux16(a=fOut, b=notFOut, sel=no, 
    out[0..7]=low8out,
    out[8..15]=high8out,
    out[15..15]=ng,
     out=out);
    //additional bits
    Or8Way(in=low8out, out=orLow );
    Or8Way(in=high8out, out=orHigh );
    Or(a= orLow, b=orHigh , out=outHas1 );
    Not(in= outHas1, out=zr );

}
